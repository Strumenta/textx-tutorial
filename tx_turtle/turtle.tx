Scene:
    /* The root rule */
    shapes*=Shape //First, define some shapes
    draw_instructions*=DrawInstruction //Then, tell the turtle to draw them
;

Shape:
    'shape' name=ID
        type=Type
        line_color=LineColor?
        fill_color=FillColor?
        speed=TurtleSpeed?
        lines+=Line?
        radius=Radius?
    'end'
;

Type:
    'type' type=PolygonType
;

PolygonType:
    'polygon' | 'circle' | 'Polygon' | 'Circle'
;

TurtleSpeed:
    'speed' speedMultiplier=INT
;

Color:
    'red' | 'green' | 'blue' | 'yellow' | 'magenta' | 'cyan' | 'black' | 'white' | /#[0-9a-f]{6}/
;

LineColor:
    'lines' color=Color
;

FillColor:
    'fill' color=Color
;

Line:
    'line' direction=Direction? length=INT?
;

Radius:
    'radius' radius=INT
;

Direction:
    bearing=Bearing | angle=Angle 
;

Bearing:
    'E' | 'NE' | 'SE' | 'W' | 'NW' | 'SW' | 'N' | 'S'
;

Angle:
    degrees=FLOAT ('Â°' | 'deg' | 'degrees' | 'degree') | radians=FLOAT ('rad' | 'radians' | 'radian')
;

DrawInstruction:
    'draw' shape=[Shape] position=Position? scale=Scale? rotation=Rotation?
;

Position:
    'at' x=INT ',' y=INT
;

Scale:
    'scale' a=FLOAT
;

Rotation:
    'rotate' b=Angle
;

Comment:
    /\/\*(.|\n)*?\*\// | /\/\/.*?$/
;
